// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int keyboard;

    /** Initializes the keyboard. */
    function void init() {
        let keyboard = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return keyboard[0];
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key, c;

        do Output.printChar(0);

        let key = 0;
        while (key = 0) {
            let key = keyboard[0];
        }
        let c = key;
        while (~(key = 0)) {
            let key = keyboard[0];
        }

        do Output.backSpace();
        do Output.printChar(c);
        return c;
    }

    function char _readChar() {
        var char key, c;

        do Output.printChar(0);

        let key = 0;
        while (key = 0) {
            let key = keyboard[0];
        }
        let c = key;
        while (~(key = 0)) {
            let key = keyboard[0];
        }

        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String input;
        var char c;

        let input = String.new(256);
        do Output.printString(message);
        let c = 0;
        while (true) {
            let c = Keyboard._readChar();
            do Output.backSpace();
            if (c = 128) { // newline
                do Output.printChar(c);
                return input;
            }
            if (c = 129) { // backspace
                if (input.length() > 0) {
                    do Output.printChar(c);
                    do input.eraseLastChar();
                }
            } else {
                do Output.printChar(c);
                do input.appendChar(c);
                if (input.length() > 255) {
                    return input;
                }
            }
        }

        return input;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String input;
        let input = Keyboard.readLine(message);
        return input.intValue();
    }
}
